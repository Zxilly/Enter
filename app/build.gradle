plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
}

def currentVersionCode = (new Date()).getTime() / 1000
def currentVersionName = new Date().toString()

android {
    compileSdk 33

    defaultConfig {
        applicationId "top.learningman.enter"
        minSdk 30
        targetSdk 33
        versionCode = currentVersionCode
        versionName = currentVersionName
    }

    signingConfigs {
        debug {
            def password
            def CI = System.getenv("CI")
            if (CI != null) {
                password = System.getenv("PASSWORD")
            } else {
                Properties properties = new Properties()
                properties.load(file('../local.properties').newDataInputStream())
                password = properties.getProperty('PASSWORD')
            }

            if (password == null || password.isEmpty()) {
                throw new Exception("Signing password not found.")
            }
            storeFile file('../release.jks')
            keyAlias 'key'
            storePassword password
            keyPassword password
        }
    }

    buildTypes {
        release {
            signingConfig signingConfigs.debug
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }

        debug {
            signingConfig signingConfigs.debug
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    buildFeatures {
        viewBinding true
    }
    namespace 'top.learningman.enter'
}

dependencies {
    implementation fileTree(include: '*.jar', dir: 'libs')
    implementation project(path: ':setupwizardlib')
    implementation 'androidx.core:core-ktx:1.9.0'
    implementation 'androidx.appcompat:appcompat:1.5.1'
    implementation 'com.google.android.material:material:1.7.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation "androidx.viewpager2:viewpager2:1.1.0-beta01"
    implementation 'androidx.lifecycle:lifecycle-livedata-ktx:2.5.1'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.5.1'

    def appCenterSdkVersion = '5.0.0'
    implementation "com.microsoft.appcenter:appcenter-analytics:${appCenterSdkVersion}"
    implementation "com.microsoft.appcenter:appcenter-crashes:${appCenterSdkVersion}"

    implementation "com.microsoft.appcenter:appcenter-distribute:${appCenterSdkVersion}"
}